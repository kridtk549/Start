//===== rAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	Ripped Cabus#GymPass	899,{
	mes "[Ripped]";
	mes "เฮ้, ไง. ผู้คนส่วนมากไม่ได้";
	mes "มีร่างกายที่แข็งแรง";
	mes "มาก่อน. เมื่อเจ้าต้องต่อสู้";
	mes "เป็นประจำ, ร่างกายของเจ้าอาจจะ";
	mes "อ่อนล้าและอ่อนแอ";
	mes "ในบางคราว. รู้ไหมว่าข้าหมายถึงอะไร ?";
	next;
	mes "[Ripped]";
	mes "เฮ้, การฝึกฝนกับข้านั้น, ข้าสามารถ";
	mes "การันตีได้ว่าเจ้าจะสามารถ";
	mes "แบกน้ำหนักได้มากขึ้น";
	mes "เพียงแค่เจ้ามี";
	mes "^FF0000Gym Pass^000000 มาด้วยทุกครั้ง,";
	mes "เราจะได้ฝึกฝนไปด้วยกัน.";
	next;
	mes "[Ripped]";
	mes "แต่อย่าเพิ่งตื่นเต้นไป:";
	mes "ไม่ว่าจะการฝึกฝนจะเป็นอย่างไร";
	mes "ข้าจะฝึกเท่ากับที่, เจ้าจะทำได้";
	mes "เจ้าเคยได้ยินไหม. การหักโหม";
	mes "หรือกระตือรือร้นเกินไปในการฝึก?";
	mes "มันอาจจะทำให้เจ้าตายได้. เข้าใจไหม ?";
	next;
	mes "[Ripped]";
	mes "ข้าจะบอกว่าเพื่อความปลอดภัย";
	mes "สำหรับเจ้าแล้ว";
	mes "ข้าจะให้เจ้าฝึก";
	mes "เพิ่มน้ำหนักแค่ ^FF000010 times^000000 เท่านั้น.";
	mes "ดังนั้น... เจ้าพร้อมแล้วรึยัง ?";
	next;
	switch(select("เอาเลย:ไม่:อืม, เหมือนพลังจะหายไป.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[Ripped]";
				mes "โอ้, ว่าไง, เราเจอกันอีกแล้ว";
				mes "เจ้ามี Gym Pass มาด้วย.";
				mes "แน่นอนว่า, ข้าจะเริ่มฝึกเจ้าล่ะน่ะ,";
				mes "และเจ้าจะรู้สึกถึงการเผาผลาญพลังงาน.";
				mes "พร้อมไหม ? เริ่มกันเลย.";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[Ripped]";
				mes "เป็นไงบ้าง, เจ้าทำมันได้";
				mes "เป็นการฝึกที่ดี.";
				mes "พวกเราได้เพิ่ม";
				mes "น้ำหนักแบกของๆเจ้าแล้ว";
				mes "^FF00000" + .@remain_carry + "^000000 ครั้งที่เหลือ";
				mes "เจ้าสามารถมาฝึกเพิ่มได้.";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,SKILL_PERM_GRANT;
				close;
			}
			else {
				mes "[Ripped]";
				mes "แย่มาก, ข้าเคยบอกเจ้าแล้ว ?";
				mes "เจ้าต้องนำ";
				mes "^FF0000Gym Pass^000000 มาด้วย";
				mes "ทุกครั้ง, และเราจะเริ่มฝึก";
				mes "เพิ่มน้ำหนักกัน.";
				close;
			}
		}
		else {
			mes "[Ripped]";
			mes "แย่มาก, ข้าไม่คิดว่าเรา";
			mes "จะฝึกไปได้มากกว่านี้";
			mes "กล้ามเนื้อของเจ้าถึงขีดจำกัดแล้ว.";
			mes "ถ้ามากกว่านี้มันจะอันตราย";
			mes "ร่างกายเจ้าต้องได้รับการหยุดพัก!";
			mes "ข้าว่าได้บอกเจ้าถึงขีดจำกัดในการฝึกแล้วน่ะ.";
			close;
		}
	case 2:
		mes "[Ripped]";
		mes "อ้าว, มันไม่ดีเลย.";
		mes "งั้นกลับมาใหม่ ถ้าเจ้า";
		mes "เกิดเปลี่ยนใจ. บอก";
		mes "กับเพื่อนของเจ้าด้วยว่า:";
		mes "ถ้าอยากแข็งแรง, ข้าช่วยได้";
		mes "แล้วเราจะแข็งแรงไปด้วยกัน.";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[Ripped]";
			mes "เกิดอะไรขึ้น ?";
			mes "กับการแบกของๆเจ้า";
			mes "กล้ามเนื้อลีบลงรึ ? โชคดี";
			mes "ของเจ้า, ไม่ต้องกังวลไป";
			mes "กล้ามเนื้อได้จดจำ. จำนวนการฝึกทั้งหมด.";
			mes "มันสามารถเรียกกลับมาได้...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[Ripped]";
			mes "เป็นไงบ้าง?";
			mes "เจ้าจะรู้สึกว่า";
			mes "กล้ามเนื้อกลับมาเหมือนเดิมอีกครั้ง,";
			mes "มันเยี่ยมไปเลย! ทำไมไม่ลอง";
			mes "แบกของอีกครั้งล่ะ, okay?";
			skill "ALL_INCCARRY",gympassmemory,SKILL_PERM_GRANT;
			close;
		}
		else {
			mes "[Ripped]";
			mes "อ่าห์...";
			mes "พวกเราไม่เคยฝึก";
			mes "ด้วยกันมาก่อน.";
			mes "เจ้าแน่ใจแล้วรึ.";
			close;
		}
	}
}
