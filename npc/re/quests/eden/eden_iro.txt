// Fallen_Angel_Wing Enchant
//============================================================
moc_para01,179,27,3	script	Dark Luhir#new10	4_F_VALKYRIE2,{
	disable_items;
	mes "[Dark Luhir]";
	mes "ซาหวาดดี~ ข้าสามารถเพิ่มความสามารถพิเศษให้กับ Fallen Angel Wing แก่ใครก็ได้.";
	next;
	if (getequipid(EQI_GARMENT) != 2589) {// Fallen_Angel_Wing
		mes "[Dark Luhir]";
		mes "โปรด ^0000ffสวมใส่ Fallen Angel Wing ไว้กับตัว^000000, แล้วข้าถึงจะออฟให้เจ้าได้.";
		close;
	}
	switch( select( "ยกเลิก", "ออฟ Fallen Angel Wing", "ลบออฟ Fallen Angel Wing" ) ) {
	case 1:
		mes "[Dark Luhir]";
		mes "ในเวลาต่อมา...";
		close;
	case 2:
		.@refine = getequiprefinerycnt(EQI_GARMENT);
		setarray .@card[0],
			getequipcardid(EQI_GARMENT,0),
			getequipcardid(EQI_GARMENT,1),
			getequipcardid(EQI_GARMENT,2),
			getequipcardid(EQI_GARMENT,3);
		mes "[Dark Luhir]";
		mes "ออฟได้ 1 ช่องเท่านั้น ถ้าเลเวลตีบวกคือ 1~6.";
		mes "ถ้าเลเวลตีบวกคือ 7~8, ถึงจะออฟได้ 2 ช่อง.";
		mes "+9 ขึ้นไปถึงจะออฟได้ 3 ช่อง.";
		next;
		mes "[Dark Luhir]";
		if (.@refine < 7)
			mes "ไอเท็มนี้มีเลเวลตีบวกต่ำกว่า 7. ออฟได้เฉพาะ ^ff0000รูสล็อตที่ 4^000000 เท่านั้น.";
		else if (.@refine < 9)
			mes "ไอเท็มนี้มีเลเวลตีบวกระหว่าง 7~8. ออฟ^ff0000รูสล็อตที่ 3 และ 4^000000 ได้.";
		else
			mes "ไอเท็มนี้มีเลเวลตีบวกมากกว่า 9. ออฟ ^ff0000รูสล็อตที่ 2, 3 และ 4^000000 ได้ทั้งสามช่อง.";
		mes "เจ้าต้องการออฟแบบไหน?";
		next;
		switch( select( "ขอหาของให้ได้ก่อน", "1.Fighting spirit", "2.Magic Attack", "3.Archery", "4.Critical", "5.MaxHP", "6.MaxSP", "7.Atk speed", "8.STR", "9.AGI", "10.VIT", "11.INT", "12.DEX", "13.LUK" ) ) {
		case 1:
			mes "[Dark Luhir]";
			mes "โปรดกลับมาหลังจากสวมใส่ไอเท็มของตัวเองแล้ว.";
			close;
		case 2:
			setarray .@enchants[0],4809,4808,4820,4821;		// Fighting Spirit 3~5 / Fighting Spirit 6
			.@string$ = "Fighting Spirit จะสุ่มค่าระหว่าง 3~5 (ATK+HIT)(โจมตีกายภาพ+แม่นยำ).";
			break;
		case 3:
			setarray .@enchants[0],4812,4826,4827,4828;		// Spell 4~6 / Spell 7
			.@string$ = "Magic spell จะสุ่มค่าระหว่าง 4~6 (MATK + Reduce change casting time)(โจมตีเวทย์+ลดระยะเวลาร่ายเวทย์).";
			break;
		case 4:
			setarray .@enchants[0],4832,4833,4834,4835;		// Expert Archer 1~3 / Expert Archer 4
			.@string$ = "Archery จะสุ่มค่าระหว่าง 1~3 (Range atack rate)(โจมตีระยะไกล).";
			break;
		case 5:
			setarray .@enchants[0],4863,4864,4865,4866;		// Fatal 1~3 / Fatal 4
			.@string$ = "Critical จะสุ่มค่าระหว่าง 1~3 (Critical damage + CRI)(ความแรงคริ+อัตราติดคริ).";
			break;
		case 6:
			setarray .@enchants[0],4861,4862,4867,4868;		// MaxHP +1~3% / MaxHP +4%
			.@string$ = "MHP จะสุ่มค่าระหว่าง 1~3 (Increase MHP %)(เพิ่มเลือดสูงสุดเป็น %).";
			break;
		case 7:
			setarray .@enchants[0],4870,4800,4871,4801;		// +25,50 or 75 sp / +100 sp
			.@string$ = "SP จะสุ่มค่าระหว่าง 25~75.";
			break;
		case 8:
			setarray .@enchants[0],4869,4872,4873,4807;		// Delayafterattack 1~3 / +1 ASPD
			.@string$ = "Attack delay จะสุ่มค่าระหว่าง 1~3 (Reduce attack delay %)(ลดดีเลย์โจมตีเป็น %).";
			break;
		case 9:
			setarray .@enchants[0],4702,4703,4704,4853;		// Str +3~5 / Special Str
			.@string$ = "STR จะสุ่มค่าระหว่าง 3~5.";
			break;
		case 10:
			setarray .@enchants[0],4731,4732,4733,4854;		// Agi +2~4 / Special Agi
			.@string$ = "AGI จะสุ่มค่าระหว่าง 2~4.";
			break;
		case 11:
			setarray .@enchants[0],4742,4743,4744,4855;		// Vit +3~5 / Special Vit
			.@string$ = "VIT จะสุ่มค่าระหว่าง 3~5.";
			break;
		case 12:
			setarray .@enchants[0],4712,4713,4714,4856;		// Int +3~5 / Special Int
			.@string$ = "INT จะสุ่มค่าระหว่าง 3~5.";
			break;
		case 13:
			setarray .@enchants[0],4722,4723,4724,4857;		// Dex +3~5 / Special Dex
			.@string$ = "DEX จะสุ่มค่าระหว่าง 3~5.";
			break;
		case 14:
			setarray .@enchants[0],4752,4753,4754,4858;		// Luk +3~5 / Special Luk
			.@string$ = "LUK จะสุ่มค่าระหว่าง 3~5.";
			break;
		}
		mes "[Dark Luhir]";
		mes .@string$ + " ^ff0000ค่าที่เลือกไม่สามารถย้อนกลับมาได้.^000000 เจ้าต้องการดำเนินการต่อหรือเปล่า?";
		next;
		if (select( "1.ออก", "2.ใช่, ข้าต้องการทำต่อ." ) == 1) {
			mes "[Dark Luhir]";
			mes "ในเวลาต่อมา...";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_GARMENT, 2589) || callfunc("F_IsEquipCardHack", EQI_GARMENT, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", EQI_GARMENT, .@refine))
			close;

		if (.@card[3] == 0) {
			.@card[3] = .@enchants[ rand(3) ];
			.@slot = 4;
		}
		else if (.@card[2] == 0 && .@refine >= 7) {
			.@card[2] = .@enchants[ rand(3) ];
			.@slot = 3;
		}
		else if (.@card[1] == 0 && .@refine >= 9) {
			.@card[1] = .@enchants[ rand(4) ];
			.@slot = 2;
		}
		else {
			mes "[Dark Luhir]";
			mes "ดูเหมือนว่าไอเท็มชิ้นนี้ได้ออฟสูงสุดแล้ว. โปรดนำชิ้นอื่นมาออฟ.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[Dark Luhir]";
		mes "เพิ่มความสามารถให้กับไอเท็มในช่องที่ ^990000" + .@slot + " แล้ว^000000.";
		delequip EQI_GARMENT;
		getitem2 2589,1,1, .@refine,0, .@card[0], .@card[1], .@card[2], .@card[3];
		close;
	case 3:
		mes "[Dark Luhir]";
		mes "การลบออฟ Fallen Angel Wing เจ้าต้องจ่าย 1,000,000 zeny?";
		mes "เจ้าต้องการดำเนินการต่อหรือเปล่า?";
		next;
		if (select( "ใช่", "ไม่ ขอบคุณ!" ) == 2)
			close;
		if (Zeny < 1000000) {
			mes "[Dark Luhir]";
			mes "เจ้าหลอกข้า เจ้ามีเงินไม่พอ!";
			close;
		}
		if (getequipid(EQI_GARMENT) == 2589) {
			.@refine = getequiprefinerycnt(EQI_GARMENT);
			.@card[0] = getequipcardid(EQI_GARMENT,0);

			mes "[Dark Luhir]";
			specialeffect2 EF_REPAIRWEAPON;
			mes "ลบออฟออกแล้ว.";
			delequip EQI_GARMENT;
			Zeny -= 1000000;
			getitem2 2589,1,1, .@refine,0, .@card[0],0,0,0;// Fallen_Angel_Wing
		}
		close;
	}
}